plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id "maven-publish"
    id 'signing'
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding = true
    }
}

def camerax_version = "1.0.2"
dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // TFLite
    implementation 'org.tensorflow:tensorflow-lite-task-vision:0.2.0'
    // Add dependency for TensorFlow Lite
    implementation 'org.tensorflow:tensorflow-lite:2.4.0'

    // Cluster
    implementation 'org.nield:kotlin-statistics:1.2.1'

    //OCR
    implementation 'com.google.android.gms:play-services-mlkit-text-recognition:18.0.0'

    //Read PDF
    implementation 'com.tom-roush:pdfbox-android:2.0.17.0'

    // CameraX library
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:1.0.0-alpha27"
}

group = "io.github.pedrolobo98"
version = "1.0.0"

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                pom {
                    name = 'SmH-MedicalDeviceScan-Library'
                    description = 'A library to read medical analyzes displayed on screens of devices such as oximeter, blood pressure meter, glucometer, thermometer and scales.'
                    url = 'https://github.com/pedrolobo98/SmH-MedicalDeviceScan-Library'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'pedrolobo98'
                            name = 'Pedro Lobo'
                            email = 'a13294@alunos.ipca.pt'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/pedrolobo98/SmH-MedicalDeviceScan-Library.git'
                        developerConnection = 'scm:git:ssh://github.com/pedrolobo98/SmH-MedicalDeviceScan-Library.git'
                        url = 'https://github.com/pedrolobo98/SmH-MedicalDeviceScan-Library/'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}